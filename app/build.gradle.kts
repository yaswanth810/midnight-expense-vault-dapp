import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt)
    id("io.github.kuiralabs.contract") version "0.1.0-alpha05"
    // Auto `adb reverse` of the localnet ports on installDebug to a physical
    // device — no manual step. No-op on emulators (they use 10.0.2.2).
    id("io.github.kuiralabs.localnet") version "0.1.0-alpha05"
}

android {
    namespace = "com.yaswanth810.expensevault"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.yaswanth810.expensevault"
        // Kuira SDK requires minSdk 30 (Block Store API, passkey
        // CredentialManager, Android 11+ scoped storage assumptions).
        minSdk = 30
        targetSdk = 36
        versionCode = 1
        versionName = "0.1.0"
    }

    buildTypes {
        debug {
            isMinifyEnabled = false
        }
        release {
            // TODO before shipping: turn on R8 + ProGuard. The starter
            // ships minify-off so newcomers see exactly the byte code
            // their source compiled to. A production app should set
            // isMinifyEnabled = true with a `proguardFiles(...)` line
            // listing the SDK's consumer rules and your own keep rules.
            isMinifyEnabled = false
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures {
        compose = true
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

// ─── Contract assets ───────────────────────────────────────────────
// The io.github.kuiralabs.contract plugin syncs the compiled Compact
// artifacts from the contract source into the app's assets before each
// build: the runtime JS as assets/runtime/counter-contract.js and the
// circuit keys as assets/keys/increment.{prover,verifier,bzkir}.
// CounterContract reads those canonical paths at runtime.
kuiraContract {
    source.set("../contract/src/managed/expense_vault")
    bundleWalletKeys.set(true)
}

dependencies {
    // Kuira SDK — one dep, full graph (Sigil identity, embedded wallet,
    // contract runtime, indexer, design system). See README "Pinned
    // versions" for the upgrade story.
    implementation(libs.kuira.dapp.ui)

    // Compose stack.
    val composeBom = platform(libs.androidx.compose.bom)
    implementation(composeBom)
    androidTestImplementation(composeBom)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.tooling.preview)
    debugImplementation(libs.androidx.compose.ui.tooling)

    // Activity + Hilt + lifecycle plumbing.
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.fragment.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.hilt.navigation.compose)
    implementation(libs.androidx.security.crypto)
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)
}
